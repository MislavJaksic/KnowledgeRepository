## [Introduction](https://www.openpolicyagent.org/docs/latest/)

Open Policy Agent (OPA) is a policy enforcement engine.  
OPA has a language to specify policy as code and APIs to offload policy decision-making from your software.  

### Overview

OPA decouples policy decision-making from policy enforcement.  
Services delegate policy queries to OPA.  
OPA generates a decision by evaluating the query input and against policies and data.  
OPA and `Rego` can describe invariants.  

### Rego

OPA policies are expressed in a declarative language called `Rego`.  

Future examples use data in `input.json`.  

#### References

```
input.servers  # -> *JSON blob*

input.servers[0].protocols[0]  # -> "https"

input.deadbeef  # -> undefined
```

#### Expressions (Logical AND)

Newline is the AND operator.  

```
input.servers[0].id == "app"
input.servers[0].protocols[0] == "https"
```

False queries are `undefined`.  

#### Variables

```
s := input.servers[0]
s.id == "app"
p := s.protocols[0]
p == "https"

s := input.servers[0]
s.id == "app"
s.protocols[1] == "telnet"

s := input.servers[0]  # error! Variables are immutable
s := input.servers[1]  # error! Variables are immutable

x := 1
x != y  # y has not been assigned a value
```

False queries are `undefined`.  

#### Iteration

```
some i
input.networks[i].public == true

some i, j
input.servers[i].protocols[j] == "http"  # find all (i, j) pairs that satisfy the condition

some i, j
id := input.ports[i].id
input.ports[i].network == input.networks[j].id
input.networks[j].public

input.servers[_].protocols[_] == "http"  # throwaway variable used only once

some i
input.servers[i].protocols[i] == "ssh"  # there is no assignment of i that satisfies the expression
```

#### Rules

Rules reuse and encapsulate logic.  
Rules are if-then statements.
Rules can either be complete or partial.  
Rules have a head and a body.  

Values generated by rules are queried via the global `data` variable.  

##### Complete Rules

Complete rules assign a single value to a variable.  

```
package example.rules
package example.constants

any_public_networks = true {  # head is true if in the body...
    net := input.networks[_]  # some network exists and..
    net.public                # it is public.
}

data.example.rules.any_public_networks

pi = 3.14  # define a constant by omitting the body
```

A rule is `undefined` if there are no variable assignments that satisfy the rule body.  

#### Partial Rules

Partial rules generate a set of values and assign them to a variable.  

```
package example.rules

public_network[net.id] {      # net.id is in the public_network set if...
    net := input.networks[_]  # some network exists and...
    net.public                # it is public.
}

some n
public_network[n]
```

#### Logical OR

Define multiple rules with the same name.  

```
package example.logical_or

default shell_accessible = false

shell_accessible = true {
    input.servers[_].protocols[_] == "telnet"
}

shell_accessible = true {
    input.servers[_].protocols[_] == "ssh"
}
```

```
package example.logical_or

shell_accessible[server.id] {
    server := input.servers[_]
    server.protocols[_] == "telnet"
}

shell_accessible[server.id] {
    server := input.servers[_]
    server.protocols[_] == "ssh"
}
```

### Putting It Together

```
1. Servers reachable from the Internet must not expose the insecure 'http' protocol.
2. Servers are not allowed to expose the 'telnet' protocol.
```

```
package example

allow = true {             # allow is true if...
    count(violation) == 0  # there are zero violations.
}

violation[server.id] {             # a server is in the violation set if...
    some server
    public_server[server]          # it exists in the 'public_server' set and...
    server.protocols[_] == "http"  # it contains the insecure "http" protocol.
}

violation[server.id] {               # a server is in the violation set if...
    server := input.servers[_]       # it exists in the input.servers collection and...
    server.protocols[_] == "telnet"  # it contains the "telnet" protocol.
}

public_server[server] {                             # a server exists in the public_server set if...
    some i, j
    server := input.servers[_]                      # it exists in the input.servers collection and...
    server.ports[_] == input.ports[i].id            # it references a port in the input.ports collection and...
    input.ports[i].network == input.networks[j].id  # the port references a network in the input.networks collection and...
    input.networks[j].public                        # the network is public.
}
```

### Running OPA

#### 1. Download OPA

##### macOS

```
$: curl -L -o opa https://openpolicyagent.org/downloads/latest/opa_darwin_amd64
```

##### Linux

```
$: curl -L -o opa https://openpolicyagent.org/downloads/latest/opa_linux_amd64

$: chmod 755 ./opa
```

#### 2. Try opa eval

`opa eval` evaluates arbitrary `Rego` expressions and policies.  

```
$: ./opa eval "1*2+3"

$: ./opa eval -i input.json -d example.rego "data.example.violation[x]"

$: ./opa eval --fail-defined -i input.json -d example.rego "data.example.violation[x]"
$: echo $?
```

#### 3. Try opa run (interactive)

`opa run` is a REPL (Read-Eval-Print-Loop).  

```
> true
true

> 3.14
3.14

> ["hello", "world"]
[
  "hello",
  "world"
]

> pi := 3.14
> pi
3.14
> pi > 3
true

> exit
```

```
$: opa run input.json

> data.servers[0].protocols[1]

> data.servers[i].protocols[j]

> net := data.networks[_]; net.public
```

```
$: opa run example.rego repl.input:input.json

> data.example.public_server[s]
```

#### 4. Try opa run (server)

```
$: ./opa run --server ./example.rego
```

```
{
    "input": <value>
}
```

```
$: cat <<EOF > v1-data-input.json
{
    "input": $(cat input.json)
}
EOF
```

```
$: curl localhost:8181/v1/data/example/violation -d @v1-data-input.json -H 'Content-Type: application/json'
$: curl localhost:8181/v1/data/example/allow -d @v1-data-input.json -H 'Content-Type: application/json'

$: curl localhost:8181 -i -d @input.json -H 'Content-Type: application/json'

$: ./opa run --server --set=default_decision=example/allow ./example.rego

$: curl localhost:8181 -i -d @input.json -H 'Content-Type: application/json'
```

#### 5. Try OPA as a Go library

OPA can be embedded inside Go programs as a library.  

```
import "github.com/open-policy-agent/opa/rego"
```

TODO
